; Inno Setup 5

; Deployment:
; 1. licenses\readme.txt: Versionsnummer setzen
; 2. deploy\readme.txt aktualisieren
; 3. grails war
; 4. Versionsnummer hier setzen
; 5. compile setup

[Code]
const compatibelDigits = 2; // Anzahl der Stellen für Datenbankkompatibilität
const Version = '0.5.6';
[Setup]
AppName=Flight Contest
AppVerName=Flight Contest 0.5.6
AppVersion=0.5.6
VersionInfoVersion=0.5.6
AppPublisher=Thomas Weise, Deutscher Präzisionsflug-Verein e.V.
AppPublisherURL=http://www.praeziflug.de
;AppUpdatesURL=file:\\
;AppSupportURL=http://

;DefaultDirName=d:\FC
DefaultDirName={pf}\Flight Contest
DefaultGroupName=Flight Contest

OutputBaseFilename=FCSetup

LicenseFile=K:\Projects\EPJ\PJ18\workspace\fc\web-app\licenses\GPL_License.txt
InfoBeforeFile=K:\Projects\EPJ\PJ18\workspace\fc\deploy\readme.txt

DisableProgramGroupPage=yes
MinVersion=0,5

[Languages]
Name: "en"; MessagesFile: "compiler:Default.isl";
; Name: "de"; MessagesFile: "German.isl";             InfoAfterFile: "K:\Projects\EPJ\PJ18\workspace\fc\deploy\readme.txt";

[Messages]
en.BeveledLabel=Flight Contest
;de.BeveledLabel=Flight Contest

[CustomMessages]
en.AdminNecessary=You need administrator privileges to install Flight Contest!
;de.AdminNecessary=Sie müssen Adminstrator-Rechte zur Installation von Flight Contest haben!

[Files]
Source: "K:\Projects\Java\jdk1.5.0_16\*";                                       DestDir: "{app}\java";           Flags: ignoreversion recursesubdirs;  Excludes: "doc,demo,include,sample,src.zip";
Source: "K:\Projects\Server\apache-tomcat-6.0.20\*";                            DestDir: "{app}\tomcat";         Flags: ignoreversion recursesubdirs;  Excludes: "docs,examples";
Source: "K:\Projects\EPJ\PJ18\workspace\fc\deploy\tomcat-users.xml";            DestDir: "{app}\tomcat\conf";
Source: "K:\Projects\EPJ\PJ18\workspace\fc\deploy\install-service.bat";         DestDir: "{app}";
Source: "K:\Projects\EPJ\PJ18\workspace\fc\target\fc-0.1.war";                  DestDir: "{app}\tomcat\webapps"; DestName: "fc.war";
Source: "K:\Projects\EPJ\PJ18\workspace\fc\deploy\readme.txt";                  DestDir: "{app}";
Source: "K:\Projects\EPJ\PJ18\workspace\fc\deploy\odbc.vbs";                    DestDir: "{app}";
Source: "K:\Projects\EPJ\PJ18\workspace\fc\deploy\AFLOS.mdb";                   DestDir: "C:\AFLOS\AFLOS_System";
Source: "K:\Projects\EPJ\PJ18\workspace\fc\web-app\licenses\GPL_License.txt";   DestDir: "{app}";                AfterInstall: MyAfterInstall;

[Run]
Filename: "wscript.exe";                    Parameters: """{app}\odbc.vbs""";      WorkingDir: "{app}";  Flags: runhidden;
Filename: "{app}\install-service.bat";      Parameters: "install FlightContest";   WorkingDir: "{app}";  Flags: runhidden;
Filename: "{app}\tomcat\bin\tomcat6w.exe";  Parameters: "//MS//FlightContest";     WorkingDir: "{app}";  Flags: nowait;

[UninstallRun]
Filename: "taskkill";                       Parameters: "/F /IM tomcat6w.exe";     WorkingDir: "{app}";
Filename: "{app}\install-service.bat";      Parameters: "remove FlightContest";    WorkingDir: "{app}";  Flags: runhidden;

[InstallDelete]
Type: filesandordirs; Name: "{app}\java";
Type: filesandordirs; Name: "{app}\tomcat";

[UninstallDelete]
Type: filesandordirs; Name: "{app}\java";
Type: filesandordirs; Name: "{app}\tomcat";

[Icons]
Name: "{group}\Flight Contest Service Manager";         Filename: "{app}\tomcat\bin\tomcat6w.exe";             Parameters: "//MS//FlightContest";    WorkingDir: "{app}";
Name: "{group}\Flight Contest";                         Filename: "http://localhost:8080/fc/contest/start";    WorkingDir: "{userdesktop}";
Name: "{group}\Präzisionsflug-Verein e.V.";             Filename: "http://www.praeziflug.de";                  WorkingDir: "{userdesktop}";
Name: "{group}\Readme";                                 Filename: "{app}\readme.txt";                          WorkingDir: "{userdesktop}";
Name: "{group}\Lizenz";                                 Filename: "{app}\GPL_License.txt";                     WorkingDir: "{userdesktop}";
Name: "{group}\Install AFLOS connection";               Filename: "wscript.exe";                               Parameters: """{app}\odbc.vbs""";     WorkingDir: "{app}";
Name: "{group}\Uninstall Flight Contest";               Filename: "{uninstallexe}"

[Code]
function InitializeSetup(): Boolean;
var lastVersion: string;
    shell: variant;
    ret: LongInt;
begin

  // Admin?
  if Not IsAdminLoggedOn Then Begin
    MsgBox( ExpandConstant('{cm:AdminNecessary}'), mbInformation, MB_OK);
    Result := false;
    exit;
  End;

  // save last version
  if RegQueryStringValue( HKLM, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\Flight Contest_is1', 'DisplayVersion', lastVersion ) Then Begin
    RegWriteStringValue( HKCU, 'SOFTWARE\Flight Contest', 'lastVersion', lastVersion )
  end;
  
  // stop tomcat
  shell := CreateOleObject('WScript.Shell');
  ret := shell.Run('cmd.exe /Ctaskkill.exe /F /IM tomcat6w.exe', 0, true);  // 0 - unsichtbar, True - Wait for return
  ret := shell.Run('cmd.exe /Ctaskkill.exe /F /IM tomcat6.exe', 0, true);  // 0 - unsichtbar, True - Wait for return
  //MsgBox( IntToStr(ret), mbInformation, MB_OK);

  Result := true;
end;

function GetCompareVersion(version : string): string;
var digit: integer;
    versionStr: string;
    versionSubStr: string;
    newVersion: string;
begin
  digit := 0;
  versionStr := version + '.';
  while ( (Pos('.', versionStr) > 0) and (digit < compatibelDigits) ) do begin
    digit := digit + 1;
    versionSubStr := Copy(versionStr, 1, Pos('.', versionStr)-1);
    versionStr := Copy(versionStr, Pos('.', versionStr)+1, Length(versionStr));
    if (newVersion = '') then begin
      newVersion := versionSubStr;
    end else begin
      newVersion := newVersion + '.' + versionSubStr;
    end;
  end;
  Result := newVersion;
end;

function IsDBCompatibel(lastVersion : string): Boolean;
begin
  if GetCompareVersion(Version) <> GetCompareVersion(lastVersion) then begin
    Result := false;
    exit;
  end;
  Result := true;
end;

function GetDateTimeStr(): string;
var time: string;
    date: string;
    dateSubStr: string;
    newDate: string;
begin
  (*
  time := GetTimeString();
  StringChange(time, ':', '.');
  
  date := GetDateString();
  while ( Pos('-', date) > 0 ) do begin
    dateSubStr := Copy(date, 1, Pos('-', date)-1);
    date := Copy(date, Pos('-', date)+1, Length(date));
    if (newDate = '') then begin
      newDate := dateSubStr;
    end else begin
      newDate := dateSubStr + '.' + newDate;
    end;
  end;
  if (date <> '') then begin
    newDate := date + '.' + newDate;
  end;

  Result := newDate + '-' + time;
  *)

  Result := GetDateTimeString('yyyy.mm.dd-hh.nn.ss', #0, #0);
end;

procedure MyAfterInstall();
var lastVersion: string;
    dbDir: string;
    dbBackupDir: string;
begin

  // get last version
  RegQueryStringValue( HKCU, 'SOFTWARE\Flight Contest', 'lastVersion', lastVersion )
  
  // get db directory
  dbDir := ExpandConstant('{app}\fc');

  // db backup
  if not IsDBCompatibel(lastVersion) then begin
    if DirExists( dbDir ) then begin
      dbBackupDir := dbDir + '-' + GetDateTimeStr();
      if lastVersion <> '' then begin
        dbBackupDir := dbBackupDir + '-' + lastVersion;
      end;
      RenameFile( dbDir, dbBackupDir );
    end;
  end;
  
  // create db directory
  if not DirExists( dbDir ) then begin
    CreateDir( dbDir );
  end;

  // write installed version
  RegWriteStringValue( HKCU, 'SOFTWARE\Flight Contest', 'lastVersion', Version )

end;




